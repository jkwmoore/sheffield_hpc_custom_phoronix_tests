; Benchmark 1000 iteration test case for ANSYS Fluent 17 and up
; Created by Fred Sonnenwald at the University of Sheffield
; This benchmark may work with older Fluent versions, but some of the TUI commands may need to be changed
; This simulation is of submerged vegetation in an infinately long flume

; To run locally, execute the following command in a shell where X is the number of cores
; fluent 3ddp -tX -i runbenchmark.jou -g

; Larger meshes (smaller cells) are suitable for testing more nodes and will use more RAM
; Expected memory usage (sum of all cores) is approximately 256(?), 120, 30, 4, and 1 GB
; Expected core count for reasonable solution time of <1 hour is 128(?), 32, 16, 8, and 2 Broadwell cores
; Limitations for this problem are guessed to be:
;  * CPU Bound for the larger meshes;
;  * Memory I/O bound for mid-size meshes; and
;  * Interconnect bound for the smaller meshes
; Load the mesh (change which mesh by commenting,uncommenting - comments start with ;)
;f rc 3d_0.0025.msh
;f rc 3d_0.0030.msh
;f rc 3d_0.0050.msh
f rc 3d_0.0100.msh
;f rc 3d_0.0200.msh

; Define water
/define/materials/copy fluid water-liquid
/define/boundary-conditions/fluid vegetation yes water-liquid n n n n 0 n 0 n 0 n 0 n 0 n 0 n n n n n
/define/boundary-conditions/fluid water yes water-liquid n n n n 0 n 0 n 0 n 0 n 0 n 0 n n n n n
/define/materials/delete air

; Setup periodic flow
mesh mz mp inlet-vegetation outlet-vegetation n y y
mesh mz mp inlet-water outlet-water n y y
/define/periodic-conditions massflow 50 , , , , , ,

; Zero-shear surface boundary condition
/define/boundary-conditions/set/wall surface () shear-bc yes shear-bc-spec-shear q

; Use the Reynolds Stress Model
/define/models/viscous/ke-rng y

; Use the Enhanced Wall Treatment near wall boundary conditions
/define/models/viscous/nwt ewt yes

; Setup drag within the vegetation with Cd=1 & a=4
/define/boundary-conditions/fluid vegetation yes water-liquid n n n n 0 n 0 n 0 n 0 n 0 n 0 n n n 
y n n 1 n 0 n 0 n 0 n 1 n 0 n n 0 n 0 n 0 n n 4 n 4 n 0 0 0 n 1 , 1
n

; Set the Coupled Pressure-Velocity Coupling and second order discritisation
/solve/set/pv-coupling 21
/solve/set/disc/pres 14
/solve/set/disc/mom 4
/solve/set/disc/k 1
/solve/set/disc/e 1
/solve/set/disc/drsm 1

; Set initial flow conditions and initialise
/solve/initialize/set-defaults/pressure 0
/solve/initialize/set-defaults/x-velocity 0.0265625
/solve/initialize/set-defaults/y-velocity 0
/solve/initialize/set-defaults/k 1e-5
/solve/initialize/set-defaults/eps 1e-5
/solve/initialize/initialize-flow

; Record the start time in a txt file
!date +"%s" >> benchmarktimes.txt

; Run for 1000 iterations
/so it 1000

; Record the end time in a txt file
!date +"%s" >> benchmarktimes.txt

; Save, overwriting the previous run if it exists
/f wcd benchmark ok

; Exit
exit ok
